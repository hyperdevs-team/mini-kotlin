apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.gladed.androidgitversion"

buildscript {
    ext {
        kotlin_version = "1.4.31"

        android_compile_sdk = 30
        android_target_sdk = 30
        android_build_tools_version = "30.0.3"

        activity_version = "1.2.1"
        fragment_version = "1.3.3"
        lifecycle_version = "2.3.1"
        coroutines_version = "1.4.3"

        kodein_version = "7.4.0"

        junit_version = "4.13.2"
        test_runner_version = "1.3.0"
        espresso_version = "3.3.0"
        kluent_version = "1.65"
    }

    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "com.gladed.androidgitversion:gradle-android-git-version:0.4.14"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.38.0"
    }
}

androidGitVersion {
    codeFormat "MNNPP"
    baseCode 1
}

allprojects {
    ext {
        mini_version = androidGitVersion.name()
        android_version_name = androidGitVersion.name()
        // The Android version is multiplied by a big number so it is placed in the leftmost part
        // of the version code because we can't add it in androidGitVersion's codeFormat parameter
        android_version_code = (android_compile_sdk * 100000) + androidGitVersion.code()
    }

    version = mini_version
    group = "com.masmovil.masmini"

    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}